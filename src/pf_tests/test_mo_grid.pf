module test_mo_grid
  use funit
  use mo_grid, only: grid_t, cartesian, top_down, bottom_up
  use mo_kind, only: i4, i8, dp
  use mo_netcdf, only: NcDataset, NcVariable, NcDimension
  use mo_constants, only: nodata_dp
  use mo_utils, only: flip, flipped
  implicit none

  private

  public :: test_grid
  public :: test_layer

contains

  @test
  subroutine test_grid()

    implicit none
    type(grid_t) :: cgrid, fgrid, rgrid, agrid
    type(NcDataset) :: nc
    type(NcDimension) :: x_dim, y_dim
    type(NcVariable) :: var
    real(dp) :: t=1.0E-5_dp
    integer(i8) :: i
    real(dp), allocatable :: dummyv1(:), dummyv2(:), dummyv3(:), dummy1(:,:), dummy2(:,:), dummy3(:,:)
    logical, allocatable :: mask1(:,:)

    call cgrid%init( &
      nx=2, ny=3, xllcorner=3973369.0_dp, yllcorner=2735847.0_dp, cellsize=72000.0_dp, coordsys=cartesian, &
      mask=reshape([.true., .true., .true., .true., .true., .false.], [2, 3]), y_direction=bottom_up)

    allocate(cgrid%lon(cgrid%nx, cgrid%ny))
    allocate(cgrid%lat(cgrid%nx, cgrid%ny))
    cgrid%lon(:,:) = reshape([ 5.8252_dp,  6.7885_dp,  5.7714_dp,  6.7470_dp,  5.7157_dp,  6.7042_dp], [2, 3])
    cgrid%lat(:,:) = reshape([47.9806_dp, 48.0130_dp, 48.6272_dp, 48.6600_dp, 49.2736_dp, 49.3069_dp], [2, 3])
    call cgrid%estimate_aux_vertices()

    fgrid = cgrid%derive_grid(downscaling_factor=3)

    ! write NetCDF
    nc = NcDataset("6x9.nc", "w")
    call cgrid%to_netcdf(nc)
    x_dim = nc%getDimension("x")
    y_dim = nc%getDimension("y")
    var = nc%setVariable("test", "f64", [x_dim, y_dim])
    call var%setFillValue(nodata_dp)
    call var%setAttribute("missing_value", nodata_dp)
    call var%setAttribute("coordinates", "lat lon")
    dummyv1 = [(real(i, dp), i=1_i8,cgrid%ncells)]
    dummy1 = cgrid%unpack(dummyv1)
    call var%setData(dummy1)
    call nc%close()

    ! read NetCDF
    call rgrid%from_netcdf("6x9.nc", var="test")

    ! ascii IO
    call rgrid%to_ascii_file("6x9.txt")
    call agrid%from_ascii_file("6x9.txt")

    ! correct reading from ascii
    @assertEqual(rgrid%y_direction, bottom_up, message='before ascii bottom_up')
    @assertEqual(agrid%y_direction, top_down, message='after ascii top_down')
    @assertEqual(rgrid%ncells, agrid%ncells)
    @assertEqual(rgrid%nx, agrid%nx)
    @assertEqual(rgrid%ny, agrid%ny)
    @assertEqual(rgrid%xllcorner, agrid%xllcorner, tolerance=t, message='ascii xllcorner compare')
    @assertEqual(rgrid%yllcorner, agrid%yllcorner, tolerance=t, message='ascii yllcorner compare')
    @assertEqual(rgrid%mask, flipped(agrid%mask, 2), message='mask compare after ascii write')

    @assertEqual(fgrid%ncells, 45_i8)
    @assertEqual(fgrid%nx, 6)
    @assertEqual(fgrid%ny, 9)
    @assertEqual(fgrid%xllcorner, cgrid%xllcorner, tolerance=t, message='xllcorner compare')
    @assertEqual(fgrid%yllcorner, cgrid%yllcorner, tolerance=t, message='yllcorner compare')

    @assertEqual(rgrid%ncells, 5_i8)
    @assertEqual(rgrid%nx, 2)
    @assertEqual(rgrid%ny, 3)
    @assertEqual(rgrid%xllcorner, cgrid%xllcorner)
    @assertEqual(rgrid%yllcorner, cgrid%yllcorner)

    @assertTrue(rgrid%has_aux_coords())
    @assertTrue(rgrid%has_aux_vertices())

    ! @assertAll
    ! @assertAny
    ! @assertAssociated
    ! @assertEqual
    ! @assertFalse
    ! @assertGreaterThan
    ! @assertGreaterThanOrEqual
    ! @assertLessThan
    ! @assertLessThanOrEqual
    ! @assertNone
    ! @assertNotEqual
    ! @assertSameShape
    ! @assertRelativelyEqual

  end subroutine test_grid

end module test_mo_grid
