enable_testing()

set (LIB_NAME forces)
find_package(PFUNIT)
# copy files
file ( COPY ./files DESTINATION ${CMAKE_CURRENT_BINARY_DIR})

if(PFUNIT_FOUND)
  set(FORCES_TEST_GLOB "*.pf" CACHE STRING "Globbing expression for pFUnit tests to run.")
  set(pfunit_suffix ".pf")
  file(GLOB testsourcefiles RELATIVE ${CMAKE_CURRENT_SOURCE_DIR} ${FORCES_TEST_GLOB})
  if (NOT FORCES_WITH_OPTIMIZATION)
    list(FILTER testsourcefiles EXCLUDE REGEX ".*mo_anneal.pf$")
    list(FILTER testsourcefiles EXCLUDE REGEX ".*mo_dds.pf$")
    list(FILTER testsourcefiles EXCLUDE REGEX ".*mo_errormeasures.pf$")
    list(FILTER testsourcefiles EXCLUDE REGEX ".*mo_mcmc.pf$")
    list(FILTER testsourcefiles EXCLUDE REGEX ".*mo_sce.pf$")
    list(FILTER testsourcefiles EXCLUDE REGEX ".*mo_opt_eval_utils.pf$")
  endif()
  if (NOT FORCES_WITH_NETCDF)
    list(FILTER testsourcefiles EXCLUDE REGEX ".*mo_ncread.pf$")
    list(FILTER testsourcefiles EXCLUDE REGEX ".*mo_netcdf.pf$")
    list(FILTER testsourcefiles EXCLUDE REGEX ".*mo_grid.pf$")
    list(FILTER testsourcefiles EXCLUDE REGEX ".*mo_grid_io.pf$")
  endif()
  foreach(testsourcefile ${testsourcefiles})
    get_filename_component(testname ${testsourcefile} NAME_WE)
    add_pfunit_ctest(${testname}
      TEST_SOURCES ${testsourcefile}
      LINK_LIBRARIES ${LIB_NAME})
    list(APPEND testnames ${testname})
  endforeach()
else()
  message(STATUS "No test program found, install pFUnit")
endif()
